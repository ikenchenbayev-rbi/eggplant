(** 
ScriptName: POSNCR
Version 1.0
Author: Sajeendra Das
CreatedOn: 4/22/25
PeerReviewedBy:
PeerReviewedOn:
EditedBy: 
EditedOn: 
Description:  Handlers specific to the POS Operations on the NCR POS platform.
**)

(**
"This software is copyright Keysight Technologies, Inc. and licensed under the terms of Keysight’s End User License Agreement. Terms of Keysight’s EULA may be found here: www.keysight.com/find/sweula By using this Software, you agree to the terms of the Keysight EULA."
**) 

(**  
loginToNCRPOS - Handler to login to the NCR POS SUT.
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to loginToNCRPOS 
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// If NCR POS is already logged in and in the New Order screen, skip steps to login
	if ImageFound(text:"New Order", waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(LeftHalf)) 
		log "NCR POS is already in Logged in State"
	else
		// Wait for the Enter Employee Number screen
		WaitFor Config().Waits.(maxWait), text:"Enter Employee Number", SearchRectangle:Config().SearchRectangles.(TopHalf)
		
		// IF any errors are seen, click to dismiss
		if ImageFound(text:"Errors have occurred", waitFor:Config().Waits.(minWait))
			click FoundImageLocation()
			wait 1
		end if
		
		//Enter Employee Code and Click OK to login
		Log "Entering Passcode"
		put DataFile().Logins.NCRPOS.employeeCode into passCode
		
		repeat for each character in passCode
			click text:it, SearchRectangle: Config().SearchRectangles.(NCRSoftKeypadLogin), waitFor: 1
			wait 1
		end repeat
		
		WaitFor Config().Waits.(maxWait), text:"OK", SearchRectangle:Config().SearchRectangles.(NCRSoftKeypadLogin)
		click FoundImageLocation()
		
		// Validate login to NCR
		assert that ImageFound(text:"New Order", waitFor:Config().Waits.(maxWait),SearchRectangle:Config().SearchRectangles.(LeftHalf)) with error "NCR POS Login Validation"
		
	end if
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end loginToNCRPOS

(**  
selectDiningOption - Handler to select the Dining Option on NCR POS, Dine In, Take Out etc.
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to selectDiningOption
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Read the DataFile to get the Column Number which has the Dining Option stored
	put DataFile().ColumnMapping.(global posPlatformName).DiningOption into excelColumnNumber
	
	// Read the Excel to get the Data Value for the Dining Option
	put Common.readDataFromExcel (excelColumnNumber) into diningOption
	
	if diningOption is not empty
		// Select the Dining Option on the screen
		Click text:diningOption, waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea)
		
		// Validate that the Main Course Selection screen has opened up
		assert that ImageFound(text:"Repeat", waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea)) with error "Dining Option Selection"
	end if
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end selectDiningOption

(**  
selectMainCourse - Handler to select the Main Course, Chicken, Seafood etc
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to selectMainCourse
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	//Select Main Course 
	// Read the DataFile to get the Column Number which has the Dining Option stored
	put DataFile().ColumnMapping.(global posPlatformName).MainCourse into excelColumnNumber
	
	// Read the Excel to get the Data Value for the Main Course
	put Common.readDataFromExcel (excelColumnNumber) into mainCourse
	
	if mainCourse is not empty
		split mainCourse by ","
		
		// Select Main Course and additional selections if any
		repeat for each item of mainCourse
			if ImageFound(text:trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRMainCourseSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
				click FoundImageLocation() 
				wait 1
			else if ImageFound(text:trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
				click FoundImageLocation()
				wait 1
			end if
		end repeat
	end if
	// Data is maintained comma-separated if multiple options are needed to be clicked. Split the string by "," and click options one by one
	
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end selectMainCourse

(**  
selectMainCourse - Handler to select the Meal like, BFLY SHRIMP, PopCorn Shrimp etc. And meal option like Combo, Regular etc.
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to selectMealAndOption
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	//Select Meal  
	// Read the DataFile to get the Column Number which has the Meal, Meal Option and Display Names stored
	put DataFile().ColumnMapping.(global posPlatformName).SelectMeal into excelColumnNumber
	put Common.readDataFromExcel (excelColumnNumber) into selectMeal
	
	put DataFile().ColumnMapping.(global posPlatformName).SelectMealOption into excelColumnNumber
	put Common.readDataFromExcel (excelColumnNumber) into selectMealOption
	
	put DataFile().ColumnMapping.(global posPlatformName).MealDisplayName into excelColumnNumber
	put Common.readDataFromExcel (excelColumnNumber) into mealDisplayName
	
	// Select Meal
	if selectMeal is not empty
		// Data is maintained comma-separated if multiple options are needed to be clicked. Split the string by "," and click options one by one
		split selectMeal by ","
		
		// Select Meal and additional selections if any
		repeat for each item of selectMeal
			if ImageFound(text:trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
				click FoundImageLocation() 
				wait 1
			end if
		end repeat
	end if
	
	
	// Select Meal Option
	if selectMealOption is not empty
		if ImageFound(text:"Would you like to make it a Large combo", waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea))
			put FoundImageInfo().imagerectangle +[0,0,200,200] into clickArea
			click text: "No", waitFor:Config().Waits.(minWait),SearchRectangle:clickArea
		end if
		
		// Click Meal Option on screen
		Click text:selectMealOption, waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes
		
	end if
	
	if selectMeal is not empty or selectMealOption is not empty
		// Validate that Meal gets added to the order
		//assert that ImageFound(text:mealDisplayName, waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCROrdersArea), validCharacters: mealDisplayName, validWords: mealDisplayName, ignoreSpaces: Yes, ignoreNewLines: Yes) with error "Meal Added to Order"
		assert that common.extendedFindText(mealDisplayName,Config().Waits.(minWait), Config().SearchRectangles.(NCROrdersArea)) is true with error "Meal Added to Order"
	end if
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end selectMealAndOption

(**  
selectFlavors - Handler to select the Flavors like BBQ Sauce, Ranch etc
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to selectFlavors
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Read the DataFile to get the Column Number which has the Flavor Options stored
	put DataFile().ColumnMapping.(global posPlatformName).Flavors into excelColumnNumber
	
	// Read the Excel to get the Data Value for the Flavors
	put Common.readDataFromExcel (excelColumnNumber) into flavors
	
	if flavors is not empty
		//Select Flavors on the Orders Section First
		WaitFor Config().Waits.(maxWait), text:"Flavor", SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes
		click FoundImageLocation()
		wait 1
		
		// Data is maintained comma-separated if multiple options are needed to be clicked. Split the string by "," and click options one by one
		split flavors by ","
		
		// Select Flavors
		repeat for each item of flavors
			// IF Flavors text is found in the orders area, click it
			if imagefound (text:"Flavor", WaitFor: Config().Waits.(minWait), SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
				click FoundImageLocation()
				wait 1
			end if
			
			// Select Flavor
			if ImageFound(text:trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes, caseSensitive: Yes)
				click FoundImageLocation() 
				wait 1
			end if
		end repeat
		
		
		// Validate Flavors are added, by ensuring "Flavor" text is not found in Orders area
		assert that not ImageFound(text:"Flavor", waitFor:2,SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes, ) with error "Side Added to Order"
	end if
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end selectFlavors

(**  
selectSides - Handler to select the Sides like Mash Gravyy, Rice etc
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to selectSides
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Read the DataFile to get the Column Number which has the Sides Options stored
	put DataFile().ColumnMapping.(global posPlatformName).Sides into excelColumnNumber
	
	// Read the Excel to get the Data Value for the Sides
	put Common.readDataFromExcel (excelColumnNumber) into sides
	
	if sides is not empty
	(*		
		//Select Sides on the Orders Section First
		WaitFor Config().Waits.(maxWait), text:"Sides", SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes
		click FoundImageLocation()
		wait 1*)
		
		// Data is maintained comma-separated if multiple options are needed to be clicked. Split the string by "," and click options one by one
		split sides by ","
		
		// Select Sides
		repeat for each item of sides
			// IF Flavors text is found in the orders area, click it
			if imagefound (text:"Sides", WaitFor: Config().Waits.(minWait), SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
				click FoundImageLocation()
				wait 1
			end if
			
			if ImageFound(text:trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
				click FoundImageLocation() 
				wait 1
			end if
		end repeat
		
		// Validate Sides are added, by ensuring "Sides" text is not found in Orders area
		assert that not ImageFound(text:"Sides", waitFor:2,SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes, ) with error "Side Added to Order"
	end if
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end selectSides


(**  
selectDrink - Handler to select the Drink like CocaCola, DrPepper etc
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to selectDrink
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Read the DataFile to get the Column Number which has the Drink Options stored
	put DataFile().ColumnMapping.(global posPlatformName).Drink into excelColumnNumber
	
	// Read the Excel to get the Data Value for the Drink
	put Common.readDataFromExcel (excelColumnNumber) into drink
	
	if drink is not empty
		
		//Select Drinks on the Orders Section First
		WaitFor Config().Waits.(maxWait), text:"Drink", SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes
		click FoundImageLocation()
		wait 1
		
		// Data is maintained comma-separated if multiple options are needed to be clicked. Split the string by "," and click options one by one
		split drink by ","
		
		
		// Select Drink by clicking the image of the drink
		repeat for each item of drink
			// IF an Image of Drink exists, click Image. If not, do an OCR search for the Drink
			if file (suiteinfo().imagesFolder&"/Icons/Drinks/"&it&".png") exists or if file (suiteinfo().imagesFolder&"/"&it) exists
				if ImageFound(image:"Icons/Drinks/"&trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea))
					click FoundImageLocation() 
					wait 1
				end if
			else
				if ImageFound(text:trim(it), waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes)
					click FoundImageLocation() 
					wait 1
				end if
			end if
			
		end repeat
		
		// Validate Sides are added, by ensuring "Sides" text is not found in Orders area
		assert that not ImageFound(text:"Drink", waitFor:2,SearchRectangle:Config().SearchRectangles.(NCROrdersArea), ignoreSpaces: Yes, ignoreNewLines: Yes, ) with error "Drink Added to Order"
	end if
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end selectDrink


(**  
proceedToTender - Handler to click the Total button to proceed to tender
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to proceedToTender
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Click Total button on the Orders Section First
	WaitFor Config().Waits.(maxWait), text:"Total", SearchRectangle:Config().SearchRectangles.(NCROrdersAreaOptions), ignoreSpaces: Yes, ignoreNewLines: Yes
	click FoundImageLocation()
	wait 1
	
	// If Loyalty prompt shows up, click Not Today
	if ImageFound(text:"Not Today", waitFor:Config().Waits.(minWait),SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea),  ignoreSpaces: Yes, ignoreNewLines: Yes,)
		click FoundImageLocation() 
		wait 1
	end if
	
	// Validate Tenders screen has opened
	assert that ImageFound(text:"Payments", waitFor:2,SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes, ) with error "Tenders Screen"
	
	//Call handler to read and save the Order ID in a global parameter
	saveOrderID
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
end proceedToTender


(**  
exactPayment - Handler to click the Exact button to pay with Exact amount of order
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to exactPayment
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Click Exact button on the Order First
	WaitFor Config().Waits.(maxWait), text:"Exact", SearchRectangle:Config().SearchRectangles.(NCRItemSelectionArea), ignoreSpaces: Yes, ignoreNewLines: Yes
	click FoundImageLocation()
	wait 1
	
	// Validate User is taken back to the New Order screen
	assert that ImageFound(text:"New Order", waitFor:Config().Waits.(maxWait),SearchRectangle:Config().SearchRectangles.(LeftHalf)) with error "NCR POS Login Validation"
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
end exactPayment


(**  
saveOrderID - Handler to read the order ID from the NCR screen and return the value, if needed for DAI
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to saveOrderID
	Log "Reading Order ID"
	put readText(Config().SearchRectangles.(NCROrderID)) into global orderID
	log "Order ID is: "&global orderID
	return global orderID
end saveOrderID
