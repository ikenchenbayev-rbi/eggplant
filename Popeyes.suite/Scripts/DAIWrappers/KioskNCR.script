(** 
ScriptName: KioskNCR
Version 1.0
Author: Sajeendra Das
CreatedOn: 4/22/25
PeerReviewedBy:
PeerReviewedOn:
EditedBy: 
EditedOn: 
Description:  Handlers specific to the POS Operations on the NCR Kiosk platform.
**)

(**
"This software is copyright Keysight Technologies, Inc. and licensed under the terms of Keysight’s End User License Agreement. Terms of Keysight’s EULA may be found here: www.keysight.com/find/sweula By using this Software, you agree to the terms of the Keysight EULA."
**) 

(**  
launchNCRKiosk - Handler to launch the NCR Kiosk SUT.
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to launchNCRKiosk 
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Connect to the Local PC as Single-SUT to launch Remote PC to connect to NCR Kiosk
	log "Performing steps to launch Remote PC and NCR Kiosk"
	Common.connectToLocalSingleSystem
	
	put DataFile().SUTNames.NCRKiosk into ncrKioskName
	
	// If Kiosk window is already opened, do nothing. Else perform putty steps to open RemotePC and access Kiosk window
	if imagefound (Text:"Order Now", waitFor: Config().Waits.(minWait), SearchRectangle: Config().SearchRectangles.(BottomHalf))
		LogSuccess "NCR Kiosk already launched"
	else if imagefound (Text:"Computer Name", waitFor: Config().Waits.(minWait))
		Log "Remote PC is open. Click to Open NCR Kiosk"
		
		// Open the NCR remotePC Connection for Kiosk
		Common.connectToRemotePCMachine ncrKioskName
	else
		// Open Chrome
		Common.launchChrome
		
		// Open Remote PC URL and login
		Common.openRemotePC
		
		// Open the NCR remotePC Connection for Kiosk
		Common.connectToRemotePCMachine ncrKioskName
	end if
	
	assert that imagefound (Text:"Order Now", waitFor: Config().Waits.(minWait), SearchRectangle: Config().SearchRectangles.(BottomHalf)) with error "Kiosk main screen"
	
	// Click to wake up the system, incase it is showing the screen saver
	click [1900,500]

	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end launchNCRKiosk

(**  
startKioskOrder - Handler to start the Order on Kiosk
@Version 1.0 4/22/2025
@ChangeReason Creation
**)
to startKioskOrder 
	Log "Running Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
	// Click on Order Now
	waitfor Config().Waits.(minWait), {Text:"Order Now", SearchRectangle: Config().SearchRectangles.(BottomHalf)}
	click FoundImageLocation()
	
	// Click on Language
	waitfor Config().Waits.(minWait), {Text:"English"}
	click FoundImageLocation()
	
	assert that imagefound (Text:"To eat in or to go", waitFor: Config().Waits.(minWait)) with error "Kiosk main screen"
	
	Log "End of Script:"&&item 1 delimited by ".script" of my name &"."& param(0)
	
end startKioskOrder